to setup_mediator
  set MEDIATOR-OFFERS []
  set MEDIATOR-CONSTRUCTIONS []
end



to mediator_add_offer [ _house _who _price]
  set MEDIATOR-OFFERS lput (list _house _who _price) MEDIATOR-OFFERS
end


to mediator_add_construction [ _patch _who _price]
  if trace [ show (word _who " wants to build in " _patch " for " _price) ]
  set MEDIATOR-CONSTRUCTIONS lput (list _patch _who _price) MEDIATOR-CONSTRUCTIONS
end



to mediator_behave
  
  ;; does offers first
  ;;humans_resolve_negotiations
  
  if not empty? MEDIATOR-CONSTRUCTIONS
  [
    mediator_build_constructions
  ]

  ;; resets the lists
  setup_mediator
end


to mediator_build_constructions
  foreach MEDIATOR-CONSTRUCTIONS
  [
    let _patch item 0 ?
    let _who item 1 ?
    let _price item 2 ? 
    if trace [ show (word "mediator building for " _who " on " _patch " for " _price) ]
    
    if [num-houses < MAX-HOUSES-IN-PROPERTY] of _who and [free] of _patch
    [

      ;; increases the monitor
      monitor_new_house_built
      
      ;; creates the house
      create-houses 1[
        ask _patch[ set free false ]
        initialize_house _patch _who _price

        ;; adds the house as a base-home
        if [base-home = nobody] of _who 
        [ 
          ask _who [ set base-home myself ] 
          set empty false
        ]
      ]
      
      ;; modifies the owner
      ask _who [ 
        set num-houses num-houses + 1 
        set money money - _price
      ]
      
      ;; modifies the patch
      ask _patch [set free false]
    ]

  ]
end

to humans_resolve_negotiations
  let messages filter [ message_get_kind ? = "RFQ" ] OBS-CURRENT-MESSAGES
  foreach messages [
    let buyer ( message_get_sender ? )
    let seller ( message_get_recipient ? )
    let price item 1 ( message_get_content ? )
    let negotiated-house item 0 ( message_get_content ? )

    if calc_negotiation_score buyer < calc_negotiation_score seller [
      set price calc_house_seller_price negotiated-house
    ]

    if [ price < money ] of buyer [
      ask negotiated-house [ set base-price price ]
      do_buy_house negotiated-house
    ]
  ]
end
